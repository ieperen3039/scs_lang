faux_program            = [ version_declaration ], { include_declaration }, { _definition }, [ program_interface, function_block ];
program_interface       = _function_interface;

(* version *)
version_dialect         = "faux";
version_declaration     = _keyword_version, version_dialect, [ version ];
version                 = version_major, ".", version_minor, ".", version_patch;
version_major           = ? NUMBER ?;
version_minor           = ? NUMBER ?;
version_patch           = ? NUMBER ?;

(* include section *)
include_declaration     = _keyword_use, include_file;
include_file            = ? STRING ?;

(* scopes and definitions *)
identifier              = ? IDENTIFIER ?;
_definition             = constant_def | scope | type_definition | enum_definition | variant_definition | implementation | function_definition;
scope                   = scope_name, "{", { _definition }, "}";
scope_name              = ? IDENTIFIER ?;
(* types cannot derive from fn_type or from buffers, but that is not a parser error *)
type_definition         = _keyword_type, base_type_decl, "=",  ( type_ref | native_decl );
field_declaration       = type_ref, identifier, ";";
(* reference to existing type *)
type_ref                = ( base_type_ref | tuple_ref | fn_type ), { buffer_symbol };
(* instantiation of a tuple *)
tuple_ref               = "[", type_ref, { ",", type_ref }, "]";
(* reference to existing base type, enum or variant. instantiates generic types *)
base_type_ref           = { scope_name, "." }, identifier, [ generic_types_inst ];
(* declaration of new type name, declares the existence of generic types *)
base_type_decl          = identifier, [ generic_types_decl ];
(* declaration of generic types *)
generic_types_decl      = "<", identifier, { ",", identifier }, ">";
(* instantion of generic types *)
generic_types_inst      = "<", type_ref, { ",", type_ref }, ">";
(* function does not actually use a generic_types_decl, only syntactically *)
fn_type                 = _keyword_fn, "<", [ "(", unnamed_parameter_list, ")" ], return_type, ">";
enum_definition         = _keyword_enum, identifier, "[", enum_value_decl, { ",", enum_value_decl }, "]";
enum_value_decl         = ? IDENTIFIER ?;
variant_definition      = _keyword_variant, identifier, [ generic_types_decl ], "[", variant_value_decl, { ",", variant_value_decl }, "]";
variant_value_decl      = identifier, "(", type_ref, ")";
(* generics are not instantiated, hence a base_type_decl *)
implementation          = _keyword_start_implementation, base_type_decl, { buffer_symbol }, "{", { function_definition }, "}";

constant_def            = _keyword_const, type_ref, identifier, "=", _expression;

(* function definitions and parameters *)
function_definition     = function_signature, ( function_block | native_decl );
function_signature      = _keyword_fn, function_name, [ generic_types_decl ], _function_interface;
function_name           = ? IDENTIFIER ?;
_function_interface     = "(", [ parameter_list ], ")", ":", return_type;
parameter_list          = parameter, { ",", parameter };
parameter               = type_ref, identifier;
unnamed_parameter_list  = type_ref, { ",", type_ref };
untyped_parameter_list  = identifier, { ",", identifier };
return_type             = type_ref | no_return_decl;

(* statements *)
function_block          = "{", statement, { statement_separator, statement }, [ statement_separator ], "}";
statement               = _value_expression, { { operator }, _function_expression }; 
_expression             = _value_expression | _function_expression;
_value_expression       = variable_name | tuple_construction | _literal; 
_function_expression    = implicit_par_lambda | explicit_par_lambda | function_call | method_call | mutator_cast | mutator_assign;
statement_separator     = ";";
variable_name           = ? IDENTIFIER ?;

(* function call statements *)
method_call             = function_name, "(", [ _argument_list ], ")";
function_call           = base_type_ref, ".", method_call;
_argument_list          = single_argument | ( named_argument, { ",", named_argument } );
single_argument         = _expression;
named_argument          = identifier, ":", _expression;

(* mutators, selectors and usage indicators *)
operator                = ? OPERATOR ?;
mutator_cast            = _usage_cast, type_ref;
mutator_assign          = _usage_assign, ( [ type_ref ], variable_name);

(* lamdas *)
(* the first statement of a implicit parameter lamdas starts with a function expression *)
implicit_par_lambda     = "{", { { operator }, _function_expression }, { statement_separator, statement }, [ statement_separator ], "}";
(* a lamda with zero parameters must have parenthesis, otherwise it is near-indistinguishable from implicit parameter lamdas *)
explicit_par_lambda     = "(", [ untyped_parameter_list ], ")", [ ":", return_type ], function_block;

(* other statements *)
tuple_construction      = "[", _value_expression, { ",", _value_expression }, "]";
_literal                = string_literal | integer_literal | float_literal;
string_literal          = ? STRING ?;
integer_literal         = ? NUMBER ?;
float_literal           = ? NUMBER ?, ".", ? NUMBER ?;

(* wrappers *)
native_decl             = _keyword_extern;

(* symbol definitions *)
_usage_assign = "=";
no_return_decl = "!";
_usage_cast = "as";
buffer_symbol = "[]";

(* keywords *)
_keyword_version = "version";
_keyword_type = "type";
_keyword_fn = "fn";
_keyword_enum = "enum";
_keyword_variant = "variant";
_keyword_extern = "extern";
_keyword_const = "const";
_keyword_this = "this";
_keyword_use = "use";
_keyword_start_implementation = "impl";