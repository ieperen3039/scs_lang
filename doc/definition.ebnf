faux_program            = [ version_declaration ], { include_declaration }, { _definition }, [ program_interface, "{", function_body, "}" ];
program_interface       = _function_interface;

(* version *)
version_dialect         = "faux";
version_declaration     = _keyword_version, version_dialect, [ version ];
version                 = version_major, ".", version_minor, ".", version_patch;
version_major           = ? NUMBER ?;
version_minor           = ? NUMBER ?;
version_patch           = ? NUMBER ?;

(* include section *)
include_declaration     = _keyword_use, include_file;
include_file            = ? STRING ?;

(* namespaces and definitions *)
identifier              = ? IDENTIFIER ?;
_definition             = constant_def | namespace | type_definition | enum_definition | variant_definition | implementation | function_definition;
namespace               = namespace_name, "{", { _definition }, "}";
namespace_name          = ? IDENTIFIER ?;
(* types cannot derive from fn_type or from buffers, but that is not a parser error *)
type_definition         = _keyword_type, base_type_decl, "=",  ( type_ref | native_decl );
field_declaration       = type_ref, identifier, ";";
(* reference to existing type *)
type_ref                = ( _singular_type_ref | result_type_ref | optional_type_ref ), { stream_symbol };
result_type_ref         = _singular_type_ref, "!", _singular_type_ref;
optional_type_ref       = _singular_type_ref, "?";
(* reference to existing base type, enum or variant. *)
_singular_type_ref      = ( base_type_ref | flag_type_ref | tuple_type_ref | fn_type );
base_type_ref           = identifier;
flag_type_ref           = _keyword_flag;
tuple_type_ref          = "[", type_ref, { ",", type_ref }, "]";
fn_type                 = _keyword_fn, "<", [ "(", unnamed_parameter_list, ")" ], return_type, ">";
(* declaration of new type name, declares the existence of generic types *)
base_type_decl          = identifier, [ generic_types_decl ];
(* declaration of generic types *)
generic_types_decl      = "<", identifier, { ",", identifier }, ">";
(* instantion of generic types *)
generic_types_inst      = "<", type_ref, { ",", type_ref }, ">";
enum_definition         = _keyword_enum, identifier, "[", enum_value_decl, { ",", enum_value_decl }, "]";
enum_value_decl         = ? IDENTIFIER ?;
variant_definition      = _keyword_variant, identifier, [ generic_types_decl ], "[", variant_value_decl, { ",", variant_value_decl }, "]";
variant_value_decl      = identifier, "(", type_ref, ")";
(* generics are not instantiated, hence a base_type_decl *)
implementation          = _keyword_start_implementation, base_type_decl, { stream_symbol }, "{", { function_definition }, "}";

constant_def            = _keyword_const, type_ref, identifier, "=", value_expression;

(* function definitions and parameters *)
function_definition     = _keyword_fn, function_name, _function_interface, "{", function_body, "}";
function_name           = ? IDENTIFIER ?;
_function_interface     = "(", [ parameter_list ], ")", ":", return_type;
parameter_list          = parameter, { ",", parameter };
parameter               = type_ref, identifier;

unnamed_parameter_list  = type_ref, { ",", type_ref };
untyped_parameter_list  = identifier, { ",", identifier };
return_type             = type_ref | no_return_type_ref;
no_return_type_ref      = "!";

(* statements *)
function_body           = statement, { statement_separator, statement }, [ statement_separator ];
statement               = value_expression, { function_expression }; 
value_expression        = function_call | variable_name | tuple_construction | _literal; 
function_expression     = function_call | implicit_par_lamda | explicit_par_lamda | mutator_cast | mutator_assign | operator_expression;
argument_expression     = function_call | variable_name | tuple_construction | _literal | implicit_par_lamda | explicit_par_lamda | mutator_cast | mutator_assign;
operator_expression     = operator, function_expression;
statement_separator     = ";";
variable_name           = ? IDENTIFIER ?;

(* function call statements *)
function_call           = { namespace_name, "." }, function_name, "(", [ _argument_list ], ")";
_argument_list          = argument, { ",", argument };
argument                = named_argument | unnamed_argument | flag_argument;
unnamed_argument        = argument_expression;
flag_argument           = "$", identifier;
named_argument          = identifier, "=", argument_expression;

(* mutators, selectors and usage indicators *)
operator                = ? OPERATOR ?;
mutator_cast            = _usage_cast, type_ref;
mutator_assign          = _usage_assign, ( [ type_ref ], variable_name);

(* lamdas *)
(* the first statement of a implicit parameter lamdas starts with a function expression *)
implicit_par_lamda      = "{", function_expression, { function_expression }, { statement_separator, statement }, [ statement_separator ], "}";
(* a lamda with zero parameters must have parenthesis, otherwise it is near-indistinguishable from implicit parameter lamdas *)
explicit_par_lamda      = "(", [ untyped_parameter_list ], ")", "{", function_body, "}";

(* other statements *)
tuple_construction      = "[", value_expression, { ",", value_expression }, "]";
_literal                = string_literal | integer_literal | dollar_string_literal | raw_string_literal;
(* any character following a '\' is escaped *)
string_literal          = ? STRING ?;
(* in addition to '\' escapes, every `${foo}` is replaced with the value of variable `foo` *)
dollar_string_literal   = "$", ? STRING ?;
(* slashes are not considered escape characters *)
raw_string_literal      = "r#", ? STRING ?, "#";
integer_literal         = ? NUMBER ?;
float_literal           = ? NUMBER ?, ".", ? NUMBER ?;

(* wrappers *)
native_decl             = _keyword_extern;

(* symbol definitions *)
_usage_assign = "=";
_usage_cast = "as";
stream_symbol = "[]";

(* keywords *)
_keyword_version = "version";
_keyword_type = "type";
_keyword_fn = "fn";
_keyword_enum = "enum";
_keyword_variant = "variant";
_keyword_extern = "extern";
_keyword_const = "const";
_keyword_this = "this";
_keyword_use = "use";
_keyword_flag = "flag";
_keyword_start_implementation = "impl";